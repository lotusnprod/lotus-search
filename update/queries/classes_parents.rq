PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>

SELECT ?class ?classLabel ?parent WHERE {
    VALUES ?instance {
        wd:Q15711994 # group of isomeric entities
        wd:Q17339814 # group or class of chemical substances
        wd:Q47154513 # structural class of chemical entities
        wd:Q55640599 # group of chemical entities
        wd:Q56256173 # class of chemical entities with similar applications or functions
        wd:Q56256178 # class of chemical entities with similar source or occurrence
        wd:Q55662456 # group of ortho, meta, para isomers
        wd:Q59199015 # group of stereoisomers
        wd:Q72070508 # group or class of chemical entities
        wd:Q74892521 # imprecise class of chemical entities
    }
    ?class wdt:P31 ?instance.
    OPTIONAL { ?class wdt:P279 ?parent. }
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
# LIMIT 10000
